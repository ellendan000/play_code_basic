allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'

    jacoco {
        toolVersion = "0.7.6.201602180812"
    }

    repositories {
        jcenter()
    }
}

ext {
    jacocoSourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    jacococlassDirectories = files(subprojects.sourceSets.main.output)
    jacocoExecutionData = files(subprojects.jacocoTestReport.executionData)
}

task jacocoRootReport(type: JacocoReport, dependsOn: test) {
    sourceDirectories = jacocoSourceDirectories
    classDirectories = jacococlassDirectories
    executionData = jacocoExecutionData

    reports {
        html.enabled = true
        html.destination "${buildDir}/reports/jacoco"
    }
}
test.dependsOn(subprojects.test)

jacocoTestCoverageVerification {
    sourceDirectories = jacocoSourceDirectories
    classDirectories = jacococlassDirectories
    executionData = jacocoExecutionData
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.50
            }

            limit {
                counter = 'BRANCH'
                minimum = 1.00
            }
        }
    }
}